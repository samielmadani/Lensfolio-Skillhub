plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
}

group = 'nz.ac.canterbury.seng302'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

wrapper {
    gradleVersion = '7.4'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Springboot
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: '2.7.2'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // HTML/CSS
    implementation 'org.webjars.npm:bootstrap-icons:1.8.1'
    implementation 'org.webjars.npm:fullcalendar:5.11.0'
    implementation 'org.webjars.npm:bootstrap:5.2.0'
    implementation 'org.webjars.npm:bootstrap-validate:2.1.3'
    implementation 'org.webjars.npm:jquery:3.6.0'
    implementation 'org.webjars:popper.js:2.9.2'
    implementation 'org.webjars.npm:croppie:2.6.5'

    // Sock JS
    implementation 'org.webjars.npm:sockjs-client:1.4.0'
    implementation 'org.webjars.npm:stomp__stompjs:3.0.1'


    // UC
    implementation 'net.devh:grpc-client-spring-boot-starter:2.13.1.RELEASE' // GRPC client dependencies
    implementation 'nz.ac.canterbury.seng302:shared:1.0' // Local package containing .proto files

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.0'

    // Database
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.0.4'
    developmentOnly 'com.h2database:h2:2.1.212'

    // Testing
    testImplementation "io.cucumber:cucumber-java:6.10.4"
    testImplementation "io.cucumber:cucumber-junit:6.10.4"
    implementation 'junit:junit:4.13.1'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

test {
    finalizedBy jacocoTestReport
}

// Jacoco test coverage configuration
jacoco {
    toolVersion = "0.8.7"
}

// select what reports Jacoco generates and where to put them
jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/jacoco/test-report.xml")
        csv.enabled false
        html.destination file("${buildDir}/jacoco/html")
    }
}

// Sonarqube specific configuration (task)
sonarqube {
    properties {
        property "sonar.projectKey", "team-200-portfolio"
        property "sonar.projectName", "team 200 - portfolio"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", "https://sonarqube.csse.canterbury.ac.nz"
        property "sonar.login", "$System.env.API"
    }
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'nz.ac.canterbury.seng302.portfolio.cucumber', 'src/test/resources']
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy cucumber
}
